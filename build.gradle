plugins {
	id 'com.github.eirnym.js2p' version '1.0'
    id 'java-library'
	id 'maven-publish'
	id 'com.jfrog.artifactory' version '4.17.2'
}

group = 'com.sample.model'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
    maven {
		url "${artifactory_contextUrl}/maven-group"
	}
}

dependencies {
	compileOnly 'javax.validation:validation-api:2.0.1.Final'
	compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.9.7'
	compileOnly 'joda-time:joda-time:2.2'
}

jsonSchema2Pojo {
	targetPackage = 'myproject'
	includeAdditionalProperties = true
	generateBuilders = true
	initializeCollections = false
	useBigDecimals = true
	formatDateTimes = true
	dateType = 'java.time.LocalDate'
	dateTimeType = 'java.time.ZonedDateTime'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
        	repoKey = version.endsWith('SNAPSHOT') || version.endsWith('S') ? 'libs-snapshot-local': 'libs-release-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }	
		defaults {
			publications('mavenJava')
			publishBuildInfo = true
			publishArtifacts = true
			publishPom = true
		}
    }
    resolve {
        repository {
            repoKey = 'maven-group'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

